mapscripts OldElderoot_SageHouse_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ELDEROOT_TOWN_STATE, 5: SageWelcome
    ]
}

script SageWelcome {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(1, moves(face_down, emote_exclamation_mark))
    waitmovement(1)
    playse(SE_PIN)
    msgbox(format("Ah, young {PLAYER}, it's so nice to finally meet you in person.\p
    Please come have a seat."))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up, walk_left * 2, walk_up * 2, face_right))
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(1, moves(face_left))
    waitmovement(1)
    msgbox(format("I know you must have so many questions, young {PLAYER}, but allow me to introduce myself first.\p
    I am known as The Sage of Elderoot, an ancient guardian watching over Old Elderoot, a long past version of the Elderoot Town you call home.\p
    This past version of Elderoot Town was created by CELEBI, the original guardian of Shizen, as a way to preserve the energy needed to sustain life here in this region.\p
    Unfortunately, CELEBI has not been seen for roughly 200 years...not since the War of Time, when the ancient beings DIALGA and PALKIA threatened the very existence of this world...\p
    ...and not since she appointed me as the new guardian of this place when she was forced to flee during the war that threatened her existence.\p
    Here in Old Elderoot lies the roots of the Primaroot Tree, the first tree on Shizen that birthed this region in its entirety...and continues to sustain it to this day.\p
    I don't know why I was chosen to guard over something so important to life itself, but I knew that I had to protect it at all costs.\p
    Now, I must ask you to do something of great importance...\p
    You being able to enter Old Elderoot confirms my fears...\p
    The energy that has sustained me for so long is beginning to wane and I fear my time is running out.\p 
    I must ask you to bring CELEBI home before I'm gone...\p
    ...before the barriers protecting this place break down completely.\p
    I'm not sure why the fates chose to bring us together, but it must have done it for a reason, and I believe you will be the one to bring balance back to the Shizen region."))
    msgbox("What do you say, {PLAYER}? Do you accept?", MSGBOX_YESNO) 
    while (var(VAR_RESULT) !=1) {
        msgbox("Are you sure? I urge you to reconsider.", MSGBOX_YESNO)
    }
    msgbox(format("Myself and Shizen are in your debt. Meet me at the cave just west of here and I will help you with your journey."))
    closemessage
    setvar(VAR_ELDEROOT_TOWN_STATE, 6)
    fadescreen(FADE_TO_BLACK)
    removeobject(1)
    fadescreen(FADE_FROM_BLACK)
    release
}

script OldElderoot_SageHouse_EventScript_Sage {
    goto_if_eq (VAR_ELDEROOT_TOWN_STATE, 6, OldElderoot_SageHouse_EventScript_MeetMe)
    goto_if_set (FLAG_SYS_POKEMON_GET, OldElderoot_SageHouse_EventScript_GoForth)
}

script OldElderoot_SageHouse_EventScript_MeetMe {
    lock
    applymovement(1, moves(face_player))
    waitmovement(1)
    msgbox(format("Don't worry, another aspect of me is waiting for you in the cave."))
    closemessage
    release
}

script OldElderoot_SageHouse_EventScript_GoForth {
    lock
    applymovement(1, moves(face_player))
    waitmovement(1)
    msgbox(format("I have given you all of the help I can. Go forth, young one, and grow strong!"))
    closemessage
    release
}